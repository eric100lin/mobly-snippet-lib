buildscript {
    repositories {
        mavenCentral()
    }
    dependencies {
        classpath 'com.google.protobuf:protobuf-gradle-plugin:0.8.16'
    }
}

apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'com.google.protobuf'

android {
    compileSdkVersion 30
    buildToolsVersion '30.0.3'

    defaultConfig {
        applicationId "com.google.android.mobly.snippet.example6"
        minSdkVersion 16
        targetSdkVersion 26
        versionCode 1
        versionName "0.0.1"
    }
    lintOptions {
        abortOnError true
        checkAllWarnings true
        warningsAsErrors true
    }
}

protobuf {
    // Configure the protoc executable
    protoc {
        // Download from repositories
        artifact = 'com.google.protobuf:protoc:3.17.3'
    }
    generateProtoTasks {
        all().each { task ->
            task.builtins {
                // Generates Python code in the output folder:
                python { }
                // Generates Java and Kotlin code in the output folder:
                java {
                    option "lite"
                }
                kotlin { }
            }
        }
    }
}

dependencies {
    // The 'implementation project' dep is to compile against the snippet lib source in
    // this repo. For your own snippets, you'll want to use the regular
    // 'implementation' dep instead:
    //implementation 'com.google.android.mobly:mobly-snippet-lib:1.3.0'
    implementation project(':mobly-snippet-lib')
    implementation "androidx.core:core-ktx:1.6.0"
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"
    implementation "com.google.protobuf:protobuf-kotlin-lite:3.17.3"
    implementation "com.google.protobuf:protobuf-javalite:4.0.0-rc-2"
}
